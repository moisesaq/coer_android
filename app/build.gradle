apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply from: "$project.rootDir/tools/versions.gradle"

android {
    compileSdkVersion compileVersion
    buildToolsVersion buildVersion

    defaultConfig {
        applicationId "moises.com.appcoer"
        minSdkVersion minVersion
        targetSdkVersion targetVersion
        versionCode releaseVersionCode
        versionName releaseVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        jackOptions {
            enabled true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //TODO change signingConfig and buildConfigField
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize '2g'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    List<String> activities = ['splash', 'onBoarding', 'login']
    sourceSets {
        main {
            activities.each { activity -> res.srcDirs "src/main/res-screen/${activity}" }
        }
        debug {
            java.srcDirs = ['src/debug/java']
            res.srcDirs "src/debug/res"
            activities.each { activity -> res.srcDirs "src/debug/res-screen/${activity}" }
        }
        release {
            java.srcDirs = ['src/release/java']
            res.srcDirs "src/release/res"
            activities.each { activity -> res.srcDirs "src/release/res-screen/${activity}" }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:' + espressoVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:' + appCompatVersion
    compile 'com.android.support:design:' + appCompatVersion
    compile 'com.android.support:cardview-v7:' + appCompatVersion
    testCompile 'junit:junit:' + junitVersion

    /*CRASH REPORTING*/
    compile 'com.google.firebase:firebase-crash:' + firebaseVersion
    /*VIEW PAGER INDICATOR*/
    compile 'me.relex:circleindicator:1.1.8@aar'
    /*TAP TARGET VIEW*/
    compile 'com.getkeepsafe.taptargetview:taptargetview:' + tapTargetViewVersion
    /*CIRCLE IMAGE VIEW*/
    compile 'com.github.siyamed:android-shape-imageview:' + circleImageViewVersion
    /*FLOATING BUTTON WITH MENU*/
    compile 'com.github.clans:fab:' + floatingButtonWithMenuVersion
    /*CUSTOM DATE PICKER*/
    compile 'com.wdullaer:materialdatetimepicker:' + customDatePickerVersion
    /*CUSTOM CHROME TABS*/
    compile 'com.android.support:customtabs:' + chromeTabsVersion
    /*RETROFIT*/
    compile 'com.squareup.retrofit2:retrofit:' + retrofitVersion
    compile 'com.squareup.retrofit2:converter-gson:' + retrofitVersion
    compile 'com.squareup.okhttp3:logging-interceptor:' + loggingInterceptorVersion
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:' + retrofitRxJava2AdapterVersion

    compile 'com.squareup.picasso:picasso:' + picassoVersion
    //compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    /*INJECTION DEPENDENCY*/
    compile 'com.jakewharton:butterknife:' + butterKnifeVersion
    annotationProcessor 'com.jakewharton:butterknife-compiler:' + butterKnifeVersion
    /*DAGGER 2*/
    compile "com.google.dagger:dagger:" + daggerVersion
    annotationProcessor "com.google.dagger:dagger-compiler:" + daggerVersion
    //provided 'javax.annotation:jsr250-api:1.0'
    /*RX JAVA*/
    compile 'io.reactivex.rxjava2:rxjava:' + rxJavaVersion
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:' + rxBindingVersion
}
apply plugin: 'com.google.gms.google-services'
