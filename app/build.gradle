apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/versions.gradle"

/** Load keystore */
def keystorePropertiesFile = rootProject.file("keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "moises.com.appcoer"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode versionCodeAppCoer
        versionName versionNameAppCoer
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            try {
                keyAlias keystoreProperties['debugKeyAlias']
                keyPassword keystoreProperties['debugKeyPassword']
                storePassword keystoreProperties['debugStorePassword']
                storeFile file('../keystore/debug.keystore')
            } catch (exception) {
                throw new InvalidUserDataException('You should define debugKeyAlias, debugKeyPassword,' +
                        'and debugStorePassword in gradle.properties. ' + exception.toString())
            }
        }
        release {
            try {
                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
                storePassword keystoreProperties['releaseStorePassword']
                storeFile file('../keystore/keystore_coer.jks')
            } catch (exception) {
                throw new InvalidUserDataException('You should define releaseKeyAlias, releaseKeyPassword,' +
                        'and releaseStorePassword in gradle.properties. ' + exception.toString())
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            signingConfig signingConfigs.debug
            buildConfigField "String", "URL_ENDPOINT", keystoreProperties['urlEndpointDebug']
            buildConfigField "String", "HEADER_AUTHORIZATION_VALUE", keystoreProperties['headerAuthorizationDebug']
        }

        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "URL_ENDPOINT", keystoreProperties['urlEndpointDebug']
            buildConfigField "String", "HEADER_AUTHORIZATION_VALUE", keystoreProperties['headerAuthorizationDebug']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    List<String> activities = ['splash', 'onBoarding', 'login', 'main']
    sourceSets {
        main {
            activities.each { activity -> res.srcDirs "src/main/res-screen/${activity}" }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:animated-vector-drawable:27.1.1'

    /** Custom chrome tabs */
    implementation 'com.android.support:customtabs:27.1.1'

    /** Crash reporting */
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-crash:16.0.0'

    /** ViewPageIndicator */
    implementation 'me.relex:circleindicator:1.1.8@aar'

    /** TapTargetView */
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.10.0'

    /** CircleImageView */
    implementation 'com.github.siyamed:android-shape-imageview:0.9.+@aar'

    /** FloatingButton with menu */
    implementation 'com.github.clans:fab:1.6.4'

    /** Custom Date Picker */
    implementation 'com.wdullaer:materialdatetimepicker:3.1.3'

    /** Retrofit **/
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    /** Glide **/
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'

    /** ButterKnife */
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    /** Dagger2 */
    implementation 'com.google.dagger:dagger-android-support:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'

    /** RxJava */
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'
}
apply plugin: 'com.google.gms.google-services'
